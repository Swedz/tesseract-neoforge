plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '1.0.14'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
}

configurations {
    runtimeClasspath.extendsFrom localImplementation
    compileClasspath.extendsFrom localImplementation
}

version = "${mod_version}+${minecraft_version}"
group = mod_group_id
base {
    archivesName = artifact_name
}

repositories {
    mavenLocal()
    maven {
        name = "Modmaven"
        url = "https://modmaven.dev"
        content {
            // GrandPower
            includeGroup "dev.technici4n"
            // Modern Industrialization
            includeGroup "aztech"
        }
    }
    maven {
        name = "shedaniel"
        url = "https://maven.shedaniel.me"
        content {
            // Cloth Config
            includeGroup "me.shedaniel.cloth"
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases/"
        content {
            // EMI
            includeGroup "dev.emi"
        }
    }
}

dependencies {
    localImplementation "dev.emi:emi-neoforge:${emi_version}+${minecraft_version}"

    if (project.runtime_include_mi.toBoolean()) {
        localImplementation "aztech:Modern-Industrialization:${modern_industrialization_version}"
        localImplementation "dev.technici4n:GrandPower:${grandpower_version}"
        localImplementation "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"
    } else {
        compileOnly "aztech:Modern-Industrialization:${modern_industrialization_version}"
        compileOnly "dev.technici4n:GrandPower:${grandpower_version}"
        compileOnly "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"
    }
}

tasks.named('jar') {
    from "LICENSE"
}

task sourceJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
    from "LICENSE"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = project.artifact_name

            artifact(sourceJar) {
                classifier "sources"
            }
        }
    }

    repositories {
        mavenLocal()

        maven {
            credentials {
                username = System.getenv("MODMAVEN_USERNAME")
                password = System.getenv("MODMAVEN_PASSWORD")
            }
            name = "modmaven"
            url = "https://modmaven.dev/artifactory/local-releases/"
        }
    }
}

if (System.getenv("CURSEFORGE_API_KEY")) {
    curseforge {
        apiKey = System.getenv("CURSEFORGE_API_KEY")

        project {
            id = project.curseforge_project
            releaseType = project.release_type
            changelogType = "markdown"
            delegate.changelog = System.getenv("MOD_CHANGELOG")

            def compatible_minecraft_versions = project.compatible_minecraft_versions.split(",")
            compatible_minecraft_versions.each { addGameVersion it }
            addGameVersion "NeoForge"
            addGameVersion "Java 21"

            relations {
                optionalDependency "modern-industrialization"
            }

            mainArtifact(tasks.named("jar").flatMap { it.archiveFile })
        }

        options {
            forgeGradleIntegration = false
        }
    }
}

if (System.getenv("MODRINTH_API_KEY")) {
    modrinth {
        token = System.getenv("MODRINTH_API_KEY")

        projectId = project.modrinth_project
        versionNumber = "${version}"
        versionName = "${project.mod_name} ${version}"
		versionType = project.release_type
        delegate.changelog = System.getenv("MOD_CHANGELOG")

        gameVersions = project.compatible_minecraft_versions.split(",")
		loaders = ["neoforge"]

		dependencies {
			optional.project "modern-industrialization"
		}

        uploadFile = tasks.jar
    }
}

neoForge {
    version = neoforge_version

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }

    validateAccessTransformers = true
    accessTransformers = [
            "src/main/resources/${mod_id}.accesstransformer.cfg"
    ]

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.minecraft_version
    }

    runs {
        configureEach {
            mods = [neoForge.mods."${mod_id}"]

            systemProperty 'forge.logging.markers', 'REGISTRIES'

            systemProperty 'forge.logging.console.level', 'debug'

            sourceSet = project.sourceSets.main
        }

        client {
            client()

            gameDirectory.set(project.getLayout().getProjectDirectory().dir('runs/client'))

            programArguments.addAll '--username', 'Swedz', '--uuid', 'a2dca537-693e-4ded-ac4b-c4006dd4d382'

            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id

            systemProperty 'mixin.env.remapRefMap', 'true'
            systemProperty 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            systemProperty 'mixin.debug.export', 'true'
            systemProperty 'mixin.debug.verbose', 'true'
        }

        server {
            server()

            gameDirectory.set(project.getLayout().getProjectDirectory().dir('runs/server'))

            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
            programArgument '--nogui'
        }

        data {
            data()

            gameDirectory.set(project.getLayout().getProjectDirectory().dir('runs/data'))

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()

            systemProperty 'mixin.env.remapRefMap', 'true'
            systemProperty 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            systemProperty 'mixin.debug.export', 'true'
            systemProperty 'mixin.debug.verbose', 'true'
        }
    }
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version                     : minecraft_version,
            minecraft_version_range               : minecraft_version_range,
            neoforge_version                      : neoforge_version,
            neoforge_version_range                : neoforge_version_range,
            loader_version_range                  : loader_version_range,
            mod_id                                : mod_id,
            mod_name                              : mod_name,
            mod_version                           : version,
            mod_authors                           : mod_authors,
            mod_description                       : mod_description,
            mod_github                            : mod_github,
            modern_industrialization_version_range: modern_industrialization_version_range
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

processResources {
    exclude(".cache")
}